apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


group = 'com.esamir.osalt'
version = '1.0.4-SNAPSHOT'

description = """Unofficial Google Voice API Library"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Unofficial Google Voice API Library'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Unofficial Google Voice API Library'
                url 'https://github.com/safaci2000/google-voice-java'

                scm {
                    connection 'scm:git:git@github.com:safaci2000/google-voice-java.git'
                    developerConnection 'scm:git:git@github.com:safaci2000/google-voice-java.git'
                    url 'scm:git:git@github.com:safaci2000/google-voice-java.git'
                    tag 'HEAD'
                }

                licenses {
                    license {
                        name 'The Apache License, LGPL Version 3.0'
                        url 'http://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'samir.faci'
                        name 'Samir Faci'
                        email 'samir@esamir.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}



tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}



repositories {
    mavenLocal()
    mavenCentral()
}



dependencies {
    compile group: 'dom4j', name: 'dom4j', version: '1.6.1'
    compile group: 'jaxen', name: 'jaxen', version: '1.1.1'
    compile group: 'net.sf.jtidy', name: 'jtidy', version: 'r938'
    compile group: 'org.json', name: 'json', version: '20150729'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
